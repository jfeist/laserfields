# Copyright (c) 2012, Johannes Feist
# licensed under the MIT open source license, see LICENSE file

ev_au=27.2113845
cmplx(n)=column(n)+{0,1}*column(n+1)
abz(n)=abs(cmplx(n))
pha(n)=arg(cmplx(n))
unroll_phase(phi)=($0==0? (ACCPHI=0,OLDPHI=0) : 1, DELPHI=phi-OLDPHI-ACCPHI,\
                    abs(DELPHI-2*pi)<abs(DELPHI) ? (ACCPHI=ACCPHI+2*pi, DELPHI=DELPHI-2*pi) : \
                   (abs(DELPHI+2*pi)<abs(DELPHI) ? (ACCPHI=ACCPHI-2*pi, DELPHI=DELPHI+2*pi) : 1), \
                   OLDPHI=OLDPHI+DELPHI)

# set gnuplot to automatically use the user-defined styles
set style increment user
set style line 1  lt 1  lw 2 pt 9  ps 2   lc rgb 'blue'
set style line 2  lt 2  lw 2 pt 5  ps 1.5 lc rgb 'red'
set style line 3  lt 3  lw 2 pt 7  ps 2   lc rgb 'dark-green'
set style line 4  lt 4  lw 2 pt 11 ps 2   lc rgb 'dark-violet'
set style line 5  lt 5  lw 2 pt 13 ps 2   lc rgb '#ff8c00' # old dark-orange - changed by gnuplot in Nov 2008!
set style line 6  lt 6  lw 2 pt 15 ps 2   lc rgb 'dark-red'
set style line 7  lt 7  lw 2 pt 3  ps 2   lc rgb 'forest-green'
set style line 8  lt 8  lw 2 pt 4  ps 2   lc rgb 'royalblue'

set tics front
set style data line
set format '%g'
set grid

set key top left maxrows 1
#set term pdfcairo enh solid size 18cm,14cm; outfi='laserfields_tests.pdf'; set out outfi
set term postscript color enhanced solid '' 10; outfi='laserfields_tests.ps'; set output outfi
set lmargin 8
set multiplot layout 4,1 title 'laser\_fields\_test1.in: different pulse shapes'
plot 'laserfields_test1_fields.dat' u 1:2 t 'E(t)'
plot 'laserfields_test1_fields.dat' u 1:3 t 'A(t)'
plot 'laserfields_test1_fields.dat' u 1:4 t 'Z(t)'
set log y
plot [][1e10:3e15] 'laserfields_test1_fields.dat' u 1:5 t 'I(t)',\
     for [ii=13:19] '' u 1:(column(ii)**2*3.509338e16) t sprintf('I_{%d}',ii-12)
unset log y
unset multiplot

set key maxrows 10 top right spacing 2
set multiplot layout 2,1 title 'laser\_fields\_test2.in: separated in time, overlapping in frequency'
plot 'laserfields_test2_fields.dat' u 1:2 t 'E(t)',\
     for [ii=6:9] '' u 1:ii t sprintf('E_{%d}(t)',ii-5)
set log y
plot [][1e-6:] 'laserfields_test2_fourier.dat' u 1:(abz(2)**2) t '|F({/Symbol w})|^2',\
     for [ii=6:12:2] '' u 1:(abz(ii)**2) t sprintf('|F_{%d}({/Symbol w})|^2',ii/2-2)
unset log y
unset multiplot

set multiplot layout 2,1 title 'laser\_fields\_test3.in: overlapping in time, separated in frequency'
plot [-16:56] 'laserfields_test3_fields.dat' u 1:2 t 'E(t)', for [ii=6:9] '' u 1:ii t sprintf('E_{%d}(t)',ii-5)
set log y
plot [:9][1e-6:] 'laserfields_test3_fourier.dat' u 1:(abz(2)**2) t '|F({/Symbol w})|^2',\
     for [ii=6:12:2] '' u 1:(abz(ii)**2) t sprintf('|F_{%d}({/Symbol w})|^2',ii/2-2)
unset log y
unset multiplot

unset lmargin
set log y
set title 'laser\_fields\_test3.in: comparison between analytical and numerical fourier transform'
set ytics nomirror
set y2tics auto
set ylabel '|F({/Symbol w})|^2'
set y2label '{/Symbol f/p}'
set xlabel '{/Symbol w} [a.u.]'
plot [0:9][1e-10:1] \
     'laserfields_test3_fourier.dat' u 1:(abz(2)**2)               t '|F_{ana}({/Symbol w})|^2' ls 1 lw 8,\
     'laserfields_test3_numerical_fourier.dat' u ($1/ev_au):($2/2) t '|F_{num}({/Symbol w})|^2' ls 5,\
     'laserfields_test3_fourier.dat' u 1:(unroll_phase(pha(2))/pi)  axis x1y2 t 'arg(F_{ana}({/Symbol w}))' ls 3 lw 8,\
     'laserfields_test3_numerical_fourier.dat' u ($1/ev_au):($3/pi) axis x1y2 t 'arg(F_{num}({/Symbol w}))' ls 2
unset log y
set output; system('ps2pdf '.outfi.' && rm '.outfi)
